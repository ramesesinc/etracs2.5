import com.rameses.annotations.*;
import com.rameses.util.*;
import java.rmi.server.*;
import com.rameses.services.extended.*

/*****************************************************************
* This is a service to build facts from the application source
******************************************************************/
public class BPApplicationFactBuilder  {

	@Resource("RuleService")	
	def ruleSvc;

	@ActiveDB("rule")
	def ruleDB;

	@ActiveDB("lob")
	def lobEm;

	@Service("NumberService")
	def numberSvc;

	@ProxyMethod
	public def createBusinessInfo ( String RULESET, dtype, objid, name, lob, value, factName ) {
		def obj = ruleSvc.createFact( RULESET, RULESET + "." + factName );
		obj.objid = objid;
		obj.name = name;
		obj.lob = lob;
		if( value ) {
			switch( dtype ) {
				case "decimal":
					obj.decimalvalue = Double.parseDouble(value+"");
					break;
				case "integer":
					obj.intvalue = Integer.parseInt(value+"");
					break;
				case "boolean":
					value = ( value == 1 || value == true ) ? "true" : "false";
					obj.booleanvalue = Boolean.parseBoolean(value);
					break;
				case "string_array":	
				case "string":
					obj.stringvalue = (String) value;
					break;
			}
		}
		return obj;
	}

	@ProxyMethod
	public def createTaxFeeAccount(String RULESET, def x) {
		if(x.amtpaid==null) x.amtpaid = 0;
		def tf = ruleSvc.createFact( RULESET, RULESET + ".TaxFeeAccount" );
		tf.acctid = x.account.objid;
		tf.type = x.account.taxfeetype;
		tf.amount = numberSvc.round(x.amount);
		tf.amtpaid = numberSvc.round(x.amtpaid);
		tf.amtdue = numberSvc.round(x.amount - x.amtpaid);
		tf.total = tf.amtdue;
		tf.account = x.account;
		tf.lob = x.lob;
		tf.receivableid = x.receivableid;
		if(tf.type == "TAX") {
			tf.paypriority = 3;
		}
		else if(tf.type == "REGFEE") {
			tf.paypriority = 2;
		}
		else {
			tf.paypriority = 1;
		}		
		return tf;
	}


	@ProxyMethod
	public def buildFact( String RULESET, def app ) {
		def lobMap = [:];
		def facts = [];
		def fapp = ruleSvc.createFact( RULESET, RULESET+".BPApplication", app );
		fapp.barangayid = app.barangay?.objid;
		facts << fapp;

		app.lobs?.each {
			def LOB = ruleSvc.createFact( RULESET, RULESET+".LOB");
			LOB.objid = it.lobid;
			LOB.lobid = it.lobid;
			LOB.name = it.name;
			LOB.classification = it.classification?.objid;
			LOB.attributes = "-" + lobEm.getAttributes( [lobid: it.lobid ] )*.name.join("-") + "-";
			LOB.assessmenttype = it.assessmenttype;
			facts << LOB;
			lobMap.put( it.lobid, LOB );
		}
		app.infos?.each {
			def dtype = it.attribute.datatype;
			def lob = null;
			if(it.lob) lob = lobMap[ it.lob.objid ];
			def f = createBusinessInfo(RULESET, dtype, it.objid, it.attribute.name, lob, it.value, "BusinessInfo");
			facts << f;
		}
		app.taxfees?.each {
			def tf = createTaxFeeAccount( RULESET, it );
			if(app.appyear) tf.year = app.appyear;
			tf.qtr = 0;
			if( tf.amtdue > 0 ) {
				facts << tf;
			}
		}
		return facts;
	}


}
