import com.rameses.annotations.*;
import com.rameses.util.*;
import java.rmi.server.*;
import com.rameses.services.extended.*;
import com.rameses.rules.common.*;
import bpls.facts.*;
import org.apache.commons.beanutils.BeanUtils;

public class BPBillingRuleService  {

	@ActiveDB("bpbilling")
	def em;

	@Resource("RuleService")
	def ruleSvc;

	@ActiveDB("rule")
	def ruleDB;

	@Service("DateService")
	def dateSvc;

	@Service("NumberService")
	def numberSvc;

	@ActiveDB("bpexpirydate")
	def expiryDate;

	@Service("BPFactBuilder")
	def appFactBuilder;


	@Env
	def env;

	String RULESET = "bpbilling";

	
	//check if there is expiry date specified, if not use the default date of 21
	private def createQtrDeadline( y, q ) { 
		def bd = dateSvc.getBeginningQtrDate( q, y, 1 );
		def qtrDate = dateSvc.parseDate( bd, null );
		def ed = expiryDate.findExpiryDate([year:y, qtr: q]);
		if(ed) {
			qtrDate.deadline = ed.expirydate;
		}	
		else {
			int mon = qtrDate.month;
			def smon = "-"+mon+"-";
			if( mon < 10) smon = "-0"+mon+"-"; 
			qtrDate.deadline = dateSvc.parseDate(y+smon+"20", null).date;
		}
		def qd = new QtrDeadline();
		BeanUtils.copyProperties( qd, qtrDate );
		return qd;
	}



	private def createBillItem( def x, def yr, def facts ) {
		if(x.amtpaid==null) x.amtpaid = 0;
		def tf = new BillItem();
		tf.acctid = x.account.objid;
		tf.type = x.account.taxfeetype;
		tf.amount = numberSvc.round(x.amount);
		tf.amtpaid = numberSvc.round(x.amtpaid);
		tf.amtdue = numberSvc.round(x.amount - x.amtpaid);
		tf.total = tf.amtdue;
		tf.account = x.account;

		if(x.lob) {
			//find the lob in the facts
			tf.lob = facts.find{ z-> (z instanceof bpls.facts.LOB) && z.lobid==x.lob.objid };
		}

		//x.objid is the receivable id.
		tf.receivableid = x.objid;
		tf.qtr = 0;
		if(yr) tf.year = yr;
		
		if(tf.type == "TAX") {
			tf.paypriority = 3;
		}
		else if(tf.type == "REGFEE") {
			tf.paypriority = 2;
		}
		else {
			tf.paypriority = 1;
		}		
		return tf;
	}

	def buildActions(def req) {
		def addItems = { tf, request ->
			request.items << [
				year: tf.year,
				qtr: tf.qtr,
				lob: (tf.lob) ? [objid: tf.lob.objid, name:tf.lob.name ] : null,
				account: tf.account,
				taxfeetype: tf.account.taxfeetype,
				amtdue: tf.amtdue,
				surcharge: tf.surcharge,
				interest: tf.interest,
				discount: tf.discount,
				total: tf.total,
				paypriority: tf.paypriority,
				duedate: tf.deadline,
				balance: tf.balance,
				receivableid: tf.receivableid,

				//this is the full amt.
				amount: tf.amount,
				amtpaid: tf.amtpaid
			];	
		}	
		def actions = new RuleAction();
		actions.addCommand( "split_by_qtr", new SplitByQtr( request:req, NS:numberSvc ));
		actions.addCommand( "calc_surcharge", new CalcSurcharge( request:req, NS:numberSvc ));
		actions.addCommand( "calc_interest",  new CalcInterest( request:req, NS:numberSvc ));
		actions.addCommand( "calc_discount",  new CalcDiscount( request:req, NS:numberSvc ));
		actions.addCommand( "collect_full",  new CollectFullPayment( request:req, addItems: addItems ));
		actions.addCommand( "collect_partial",  new CollectPartialPayment( request:req, addItems: addItems, NS:numberSvc ));
		actions.addCommand( "collect_excess",  new CollectExcess( request:req, NS:numberSvc ));
		actions.addCommand( "print_test", new PrintTest());
		return actions;
	}

	/**********************************************
	* The parameter here is the application
	**********************************************/
	@ProxyMethod
	public def getBillItems(def o) throws Exception {
		if(!o.billdate)
			throw new Exception("Please specify billdate");
		if(!o.application)
			throw new Exception("Please specify application");
		if(!o.application.appyear)
			throw new Exception("Please provide an app year in application");
		if(!o.payoption)
			throw new Exception("Please specify payoption");
		if(!o.application.taxfees || !o.application.taxfees.findAll{ (it.amount-it.amtpaid) > 0 }  )
			throw new Exception("No receivables found for this application");

		//this will be used in excess payment. do not remove.
		def billdt = dateSvc.parse("yyyy-MM-dd", o.billdate );

		def facts = [];
		def app = o.application;
		
		appFactBuilder.buildApplication( app, facts );
		//build the billitems
		app.taxfees.each {
			def tf = createBillItem( it, app.appyear, facts );
			if( tf.amtdue > 0 ) {
				facts << tf;
			}
		}		
		
		def billdate = new BillDate( billdt );
		if( o.partialpmt > 0 ) {
			facts << new Payment( o.partialpmt );	
		}
		facts << new EffectiveDate( billdt );
		facts << new CurrentDate( dateSvc.serverDate );
		facts << billdate;
		facts << createQtrDeadline( app.appyear, 1 );
		facts << createQtrDeadline( app.appyear, 2 );
		facts << createQtrDeadline( app.appyear, 3 );
		facts << createQtrDeadline( app.appyear, 4 );
		facts << new PayOption( o.payoption, ((o.qtr==null)?0:o.qtr) );
		
		def request = [items: [], facts: facts ];
		def actions = buildActions( request );

		//******************* EXECUTE RULES *******************************************
		def grps = ruleDB.getRulegroups( [ruleset: RULESET] );
		for( g in grps ) { 
			ruleSvc.execute( RULESET, facts, actions, g.name );
		}
		ruleSvc.execute( RULESET, facts, actions, "apply-payment" );
		//****************************************************************************
		
		//sort the output
		def items = request.items?.sort{ it.duedate };
		def result = [items: items];	

		//calculate quarter totals
		def fq1 = items.findAll{ it.qtr <= 1}?.sum{ it.total }; 
		result.total_q1 = (fq1==null) ? 0.0 : fq1;

		def fq2 = items.findAll{ it.qtr == 2}?.sum{ it.total }; 
        result.total_q2 = (fq2==null) ? 0.0 : fq2;

        def fq3 = items.findAll{ it.qtr == 3}?.sum{ it.total };  
        result.total_q3 = (fq3==null) ? 0.0 : fq3;

 		def fq4 = items.findAll{ it.qtr == 4}?.sum{ it.total };   
        result.total_q4 = (fq4==null) ? 0.0 : fq4; 

        def amtdue = items.sum{ it.amtdue }; 
        result.total_amtdue = (amtdue==null) ? 0.0 : amtdue; 

		def disc = items.sum{ it.discount }; 
        result.total_discount = (disc==null) ? 0.0 : disc; 

        def surch = items.sum{ it.surcharge }; 
        result.total_surcharge = (surch==null) ? 0.0 : surch; 

        def interest = items.sum{ it.interest }; 
        result.total_interest = (interest==null) ? 0.0 : interest; 

 		def tot = items.sum{ it.total };   
        result.total = (tot==null) ? 0.0 : tot; 

		result.validUntil = billdate.validUntil;
		result.taxcredit = request.taxcredit;
		return result;
	}

}	

/********************************************************
* specify which taxfees will be penalized in qtr basis
*********************************************************/
public class SplitByQtr implements RuleActionHandler {
	def request;
	def NS;
	public void execute(def params, def drools) {
		def tf = params.billitem;
		drools.retract( tf );
		request.facts.remove( tf );

		def amount = tf.amount;
		def amtpaid = tf.amtpaid;
		def divisor = NS.round( amount / 4 );

		for( int i=1; i<=4; i++) { 
			def amt = ((i==4) ? amount : divisor);
			amount = NS.round(amount-divisor);  
			if( amtpaid >= amt ) {
				amtpaid = NS.round(amtpaid - amt);
				continue;
			}	
			def _tf = new BillItem();
			_tf.acctid = tf.acctid;
			_tf.type = tf.type;
			_tf.amount = amt;
			_tf.amtpaid = amtpaid;
			_tf.amtdue = amt - amtpaid;
			_tf.total = amt - amtpaid;
			_tf.qtr = i;
			_tf.year = tf.year;
			_tf.expired = false;
			_tf.account = tf.account;
			_tf.lob = tf.lob;
			_tf.receivableid = tf.receivableid;
			_tf.paypriority = tf.paypriority;
			request.facts << _tf;
			drools.insert( _tf );
			amtpaid = 0;		    
		}
	}
}


//Loads each variable and 
public class CalcSurcharge implements RuleActionHandler {
	def request;
	def NS;
	public void execute(def params, def drools) {
		def tf = params.billitem;
		def amt = params.amount.doubleValue;
		tf.surcharge = NS.round(amt);
		tf.total += NS.round( tf.surcharge );
	}
}

public class CalcInterest implements RuleActionHandler {
	def request;
	def NS;
	public void execute(def params, def drools) {
		def tf = params.billitem;
		def amt = params.amount.doubleValue;
		tf.interest = NS.round(amt);
		tf.total += NS.round( tf.interest );
	}
}

public class CalcDiscount implements RuleActionHandler {
	def request;
	def NS;
	public void execute(def params, def drools) {
		def tf = params.billitem;
		def amt = params.amount.doubleValue;
		tf.discount = NS.round(amt);
		tf.total -= NS.round( tf.discount );
	}
}

public class CollectFullPayment implements RuleActionHandler {
	def request;
	def addItems;
	public void execute(def tf, def drools) {
		addItems( tf, request );
	}
}

public class CollectPartialPayment implements RuleActionHandler {
	def request;
	def addItems;
	def NS;
	public void execute(def m, def drools) {
		def tf = m.billitem;
		def partialpmt = m.amtpaid;
		def oldAmt = tf.total;

		//distribute amount partially
		tf.amtdue = NS.round( tf.amtdue / tf.total * partialpmt );
		if( tf.surcharge > 0.0 ) {
			tf.surcharge = NS.round( tf.surcharge / tf.total * partialpmt );
		}
		if( tf.interest > 0.0 ) {
			tf.interest = NS.round( partialpmt - tf.amtdue - tf.surcharge );
		}
		if( tf.discount > 0.0 ) tf.discount = NS.round( tf.discount / tf.total * partialpmt )
		tf.total = NS.round( tf.amtdue + tf.surcharge + tf.interest - tf.discount );
		tf.balance = NS.round( oldAmt - tf.total );
		addItems( tf, request );
	}
}

public class CollectExcess implements RuleActionHandler {
	def request;
	def NS;
	public void execute(def m, def drools) {
		println "process excess for tax credit " + m.excess;
		request.taxcredit =  m.excess;
	}
}

public class PrintTest implements RuleActionHandler {
	public void execute(def params, def drools) {
		def msg = params.message;
		println "-------------- Print Message ------------------";
		println msg.stringValue; 
	}
}
