import com.rameses.annotations.*;
import com.rameses.util.*;
import java.rmi.server.*;
import com.rameses.services.extended.*;

public class CityEngineerBPApplicationInterceptor {

	@ActiveDB("business_cityengineer")
	def em;

	@ActiveDB("bpapplication")
	def appDB;

	@After( pattern="BPApplicationService.create" )
	public void onSubmitForAssessment(evt) {
		def app = evt.args[0];
		def m = app.appinfos.find{ it.attribute.objid == "REQUIRE_CEO" };

		//if this info does not exist or is not supplied, we assume there is a city engineer component
		//find if there is  already a link, if not, we need to create one
		if( m?.value == true ) {
			def z = em.findInfo([businessid:app.businessid]);
			if(!z) {
				z = [objid: "BUSCEO"+ new UID()];
				z.businessid = app.businessid;
				z.state = 'PENDING';
				em.create(z);
			}
		}
	}	

	@Before( pattern="BusinessWorkflowService.submitForApproval" )
	public void onSubmitApproval(evt) {
		def app = evt.args[0];
		def z = em.findInfo([businessid:app.businessid]);
		if(z) {
			if( z.state == 'PENDING') {
				throw new Exception("There is still a pending assessment in City Engineers Office");
			}
			def tf = appDB.read( [objid:app.appid] );
			if( !tf.taxfees.find{it.department=='CEO'}) {
				throw new Exception("There must be at least one fee from the city engineers office. Please run calculate charges again");
			}
		}
	}	

	/***
	* add additional fees from city engineer. Add @Before so it can be totalled.
	***/
	@Before( pattern="BPAssessmentService.assess" )
	public void fireAssess(evt) {
		def app = evt.args[0];	
		def taxfees = em.getFees([businessid:app.businessid]);
		if( app.taxfees == null ) app.taxfees = [];
		taxfees.each {
			if(it.state.toLowerCase()=='active') {
				it.objid = "BCEOFEE"+new UID();
				it.department = 'CEO';
				it.account.taxfeetype = 'OTHERCHARGE';
				it.taxfeetype = 'OTHERCHARGE';
				app.taxfees << it;
			}
		}
	}	

	/***
	* add additional fees from city engineer. Add @Before so it can be totalled.
	***/
	@After( pattern="BusinessWorkflowService.(open|findByBIN)" )
	public void onOpen(evt) {
		def result = evt.result;
		def z = em.findInfo( [businessid: result.objid ] );
		if(z) {
			result.cityengineer = z;
		}
	}	

}