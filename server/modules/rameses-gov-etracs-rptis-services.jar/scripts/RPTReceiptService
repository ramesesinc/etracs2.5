import com.rameses.annotations.*
import com.rameses.common.*
import com.rameses.services.extended.*

class RPTReceiptService
{
	@Env 
	def env 

	@ActiveDB('cashreceiptrpt')
	def em
    
    @Service('DateService')
	def dtSvc

	@Service('NumberService')
	def numSvc

	@Service('RPTUtil')
	def util
	
	@Service('Var')
	def var 

	@Service('RPTBillingService')
	def billingSvc

	def SCHEMA_RECEIPT 		= 'cashreceiptrpt'
	def SCHEMA_ITEM 		= 'cashreceiptrptitem'

	def RECEIPT_TYPE_ONLINE = 'online'
	def RECEIPT_TYPE_MANUAL = 'manual'


	@ProxyMethod
	public def open( receipt ) {
		def rct = em.read(receipt)	
		if (rct){
			receipt.putAll(rct)
		}

		def checks = []
		receipt.paymentitems.each{
		  checks << it.bank + ' - ' + it.checkno + ' / ' + dtSvc.format('MM-dd-yyyy', dtSvc.parse('yyyy-MM-dd', it.checkdate))
		}
		receipt.checkno = null
		if (checks){
			receipt.checkno = checks.join('\n')
		}
	}


	@ProxyMethod
	public def getItemsForPayment( bill ) {
		def params = billingSvc.buildParams(bill)
		def items =  em.getItemsForPayment(params).each{ it.pay = true }	
		items.removeAll(items.findAll{it.amount == 0.0})
		if (!items) throw new Exception('There are no more unpaid ledgers for this taxpayer.')
		return items 
	}

	@ProxyMethod
	public def getItemsForPaymentByBill(bill){
		def items =  em.getItemsForPaymentByBill(bill).each{ it.pay = true }	
		items.removeAll(items.findAll{it.amount == 0.0})
		if (!items) throw new Exception('There are no more unpaid ledgers for this taxpayer.')
		return items 
	}


	@ProxyMethod
	public def createReceipt( receipt ) {
		def pdate = dtSvc.parseDate(receipt.txndate, null)
		receipt.year = pdate.year
		receipt.qtr = pdate.qtr
		receipt.month = pdate.month
		receipt.day = pdate.day
		
		em.create(receipt)
		
		//insert cash items
		receipt.rptitems.each{
			def partialbasic = 0.0
			def partialbasicint = 0.0
			def partialbasicdisc = 0.0
			def partialsef = 0.0
			def partialsefint = 0.0
			def partialsefdisc = 0.0

			if (it.partialled){
				partialbasic = it.partialbasic
				partialbasicint = it.partialbasicint
				partialbasicdisc = it.partialbasicdisc
				partialsef = it.partialsef
				partialsefint = it.partialsefint
				partialsefdisc = it.partialsefdisc
			}
			def params = [
				objid			: util.generateId('RI'), 
				rptreceiptid    : receipt.objid,
				rptledgerid		: it.rptledgerid,
				fromyear		: it.fromyear,
				fromqtr 		: it.fromqtr,
				toyear			: it.toyear,
				toqtr 			: it.toqtr,
				partialbasic 	: partialbasic,
				partialbasicint : partialbasicint,
				partialbasicdisc : partialbasicdisc,
				partialsef 		: partialsef,
				partialsefint 	: partialsefint,
				partialsefdisc 	: partialsefdisc,
				partialledyear  : it.partialledyear,
				partialledqtr   : it.partialledqtr,
				lastbilledyear  : it.toyear,
				lastbilledqtr   : it.toqtr,
				billid 			: it.billid,
			]

			em.insertPaidItemByLedgerId(params)
			em.deletePaidBillItems(params)
			em.deleteRptBillLedgers(params)
			em.deleteRptBill(params)
			em.updateLedgerYearQtrPaid(params)
		}
	}


	@ProxyMethod
	public def getSummarizedCashReceiptItems( receipt ) {
		def ledgerids = receipt.rptitems.collect{ "'" + it.rptledgerid + "'"}.join(',')
		def filter = "rl.objid  IN (${ledgerids})"
		def items = []
		em.getSummarizedCashReceiptItems([filter:filter]).each{
			it.objid = 'CI' + new java.rmi.server.UID()
			it.receiptid = receipt.objid
			items += it
		}
		return items.findAll{it.amount > 0.0} 
	}



	@ProxyMethod
	public void voidReceipt( receipt ) {
		def params = [
			rptreceiptid : receipt.objid,
		]

		em.getLedgersMinPaidYearAndQtr(params).each{
			it.toyear = (it.minqtr <= 1 ? it.minyear - 1 : it.minyear) 
			it.toqtr = (it.minqtr <= 1 ? 4 : it.minqtr - 1) 
			it.lastbilledyear = null
			it.lastbilledqtr = null
			it.partialledyear = it.minyear
			it.partialledqtr = it.fromqtr 
			
			def prevrct = em.findReceiptPartialInfo(it)

			if (! prevrct ){
				prevrct = [
					partialbasic : 0.0,
					partialbasicint : 0.0,
					partialbasicdisc : 0.0,
					partialsef : 0.0,
					partialsefint : 0.0,
					partialsefdisc : 0.0,
				]
			}
			it.putAll(prevrct)

			em.resetVoidedLedgerInfo(it)
			//billingSvc.generateBillByLedgerId(it.rptledgerid)
		}
	}


	@ProxyMethod
	public def getItemsForPrinting(receipt) {
		def items = em.getItemsForPrinting([rptreceiptid:receipt.objid])
		items.each{
			if (it.owner.name == receipt.paidby) {
				it.owner.name = ''
			}
		}
		return items 
	}


	@ProxyMethod
	public def getCollectionsByCount(receiptcount){
		def params = [userid:env.USERID, _limit:receiptcount, _pagingKeys:'cr.objid']
		return em.getCollectionsByCount(params)
	}


}
