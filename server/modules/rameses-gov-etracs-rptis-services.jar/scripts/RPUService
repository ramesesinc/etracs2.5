import com.rameses.annotations.*
import com.rameses.common.*;
import com.rameses.util.*;
import com.rameses.services.extended.*;


class RPUService
{
    @ActiveDB('rpu')
    def em

    @Service('PropertyClassificationService')
    def classSvc 

    @Service('ExemptionTypeService')
    def exemptSvc 

    @Service('RPTUtil')
    def util 

    
    def RPUTYPE_LAND       = 'land' 
    def RPUTYPE_BLDG       = 'bldg'
    def RPUTYPE_MACH       = 'mach'
    def RPUTYPE_PLANTTREE  = 'planttree'
    def RPUTYPE_MISC       = 'misc' 

    def STATE_INTERIM       = 'INTERIM'
    def STATE_CURRENT       = 'CURRENT'
    def STATE_CANCELLED     = 'CANCELLED'
    
    
    @ProxyMethod
    public def initRpu( info ) {
        def rputype = getTypeFromSuffix( info.suffix )

        def rpu = [
            objid           : util.generateId('RPU'),
            state           : STATE_INTERIM,
            ry              : util.toInteger(info.ry),
            suffix          : info.suffix,
            subsuffix       : info.subsuffix,
            fullpin         : info.fullpin,
            rputype         : rputype,
            taxable         : true,
            totalareaha     : 0.0,
            totalareasqm    : 0.0,
            totalbmv        : 0.0,
            totalmv         : 0.0,
            totalav         : 0.0,
            hasswornamount  : false,
            swornamount     : 0.0,
            useswornamount  : false,
            reclassed       : false,
            isnew           : true,
            realpropertyid  : info.realpropertyid,
        ]
        rpu.rpumasterid = rpu.objid 
        rpu.fullpin = info.fullpin 
        checkDuplicateFullPin( rpu )
        return rpu
    }    


    /*================================================================================
    **
    ** MOST METHODS ARE INTERCEPTED BY SPECIFIC LGU TYPES FOR ITS IMPLEMENTATIONS
    **
    ================================================================================*/

    @ProxyMethod
    public def calculateAssessment( rpu ) {
        return rpu    
    }


    @ProxyMethod
    public def generateNewIds( rpu ) {
        rpu.previd = rpu.objid 
        rpu.objid = util.generateId('RPU')
        return rpu 
    }


    @ProxyMethod
    public def reviseRpu(rpu){
        return rpu 
    }


    @ProxyMethod
    public def createRpu(rpu) {
        createRpuMaster(rpu)
        checkDuplicateFullPin(rpu)
        rpu.state = STATE_INTERIM
        em.create(rpu)
        return rpu 
    }


    @ProxyMethod
    public def updateRpu( rpu ) {
        em.updateImmediate(rpu)
        return rpu
    }


    @ProxyMethod
    public def openRpu( rpu ) {
        rpu = em.read(rpu)
        if (!rpu) throw new Exception('Cannot open RPU. The record does not exist or has already been deleted.')

        rpu.classification  = classSvc.findById(rpu.classification.objid)
        rpu.exemptiontype   = exemptSvc.findById(rpu.exemptiontype.objid)
        rpu.taxable         = util.toBoolean(rpu.taxable, true)
        rpu.hasswornamount  = util.toBoolean(rpu.hasswornamount, false)
        rpu.useswornamount  = util.toBoolean(rpu.useswornamount, false)
        rpu.reclassed       = util.toBoolean(rpu.reclassed, false)
        return rpu 
    }


    @ProxyMethod
    public def deleteRpu( rpu ) {
        def retval = em.deleteRpu(rpu)
        if (retval == 0) 
            throw new Exception('Cannot delete record. RPU has already been approved or cancelled.')
        
        deleteRpuMaster(rpu)
    }


    @ProxyMethod
    public def approveRpu( rpu ) {
        checkLandStateCurrent(rpu)

        rpu.prevstate = STATE_INTERIM
        rpu.state = STATE_CURRENT
        def retval = em.updateRpuState(rpu)
        if (retval == 0) 
            throw new Exception('Cannot approve record. RPU has already been approved or cancelled.')

        updateImprovementLandRpuIds(rpu)
        return rpu 
    }


    /*---------------------------------------------
    * Update Improvement landrpuid reference to 
    * this current landrpu.objid 
    -----------------------------------------------*/
    @ProxyMethod
    public void updateImprovementLandRpuIds(rpu){
        if (rpu.rputype != RPUTYPE_LAND) 
            return;
        
        def params = [landrpuid:rpu.objid, realpropertyid:rpu.realpropertyid]
        em.updateBldgRpuLandRpuId(params)
        em.updateMachRpuLandRpuId(params)
        em.updatePlantTreeRpuLandRpuId(params)
        em.updateMiscRpuLandRpuId(params)
    }



    /*----------------------------------------------------------
    *
    * SUPPORT METHODS
    *
    ------------------------------------------------------------*/
    @ProxyMethod
    public List getRpuTypes(  ) {
        return [RPUTYPE_LAND, RPUTYPE_BLDG, RPUTYPE_MACH, RPUTYPE_PLANTTREE, RPUTYPE_MISC]
    }


    @ProxyMethod
    public def getTypeFromSuffix( suffix ) {
        def type = null
        if (suffix == 0) 
            type = RPUTYPE_LAND
        else if (suffix >= 1001 && suffix <= 1999) 
            type = RPUTYPE_BLDG
        else if (suffix >= 2001 && suffix <= 2999) 
            type = RPUTYPE_MACH
        else if (suffix >= 3001 && suffix <= 6999) 
            type = RPUTYPE_PLANTTREE
        else if (suffix >= 7001 && suffix <= 7999) 
            type = RPUTYPE_MISC
        else 
            throw new Exception('Suffix No. ' + suffix + ' is invalid or not supported.')
        return type 
    }


    @ProxyMethod
    public void validateSuffix( rputype, suffix ) { 
        def type = getTypeFromSuffix(suffix)
        if (type != rputype)
            throw new Exception('Suffix is invalid for property type ' + rputype + '.')
    }


    @ProxyMethod
    public List getClassifications(  ) {
        return classSvc.getClassifications([:])    
    }


    @ProxyMethod
    public List getExemptionTypes(){
        return exemptSvc.getExemptionTypes([:])
    }


    /* City, Municipality or Province interceptor build actual lists */
    @ProxyMethod
    public List getLgus( lgutype ) {
        return []
    }
    

    /*----------------------------------------------------------
    *
    * HELPER METHODS
    *
    ------------------------------------------------------------*/

    void checkLandStateCurrent(rpu){
        if (rpu.rputype != RPUTYPE_LAND) {
            def landrpu = em.findLandRpuById([objid:rpu.landrpuid])
            if (!landrpu){
                landrpu = em.findLandRpuByRealPropertyId([realpropertyid:rpu.realpropertyid])
            }
            if (!landrpu) {
                throw new Exception('Land RPU does not exist.')
            }

            if (landrpu.state == STATE_CANCELLED)
            throw new Exception('Land RPU has already been cancelled.')

            if (landrpu.state != STATE_CURRENT)
            throw new Exception('Land RPU is not yet current.')
        }
    }


    void createRpuMaster(rpu){
        try {
            if (rpu.rpumasterid == null ){
                rpu.rpumasterid = rpu.objid 
            }
            em.createRpuMaster([objid:rpu.rpumasterid])
        }
        catch(e){
            //ignore possible duplicate when there are multiple assessments
        }
    }


    void deleteRpuMaster(rpu){
        try{
            em.deleteRpuMaster([objid:rpu.rpumasterid])
        }
        catch(e){
            //ignore: possible references when there are multiple assessments
        }
    }


    /*------------------------------------------
    * 
    * Check duplicate if previd is null. 
    * Null previd represents a new rpu
    *
    ------------------------------------------*/
    @ProxyMethod
    public void checkDuplicateFullPin( rpu ){
        if (! rpu.previd ) {
            def data = em.findDuplicateFullPin([objid:rpu.objid, ry:rpu.ry, fullpin:rpu.fullpin])
            if (data){
                throw new Exception('PIN ' + rpu.fullpin + ' already exist.' )
            }
        }
    }

}
