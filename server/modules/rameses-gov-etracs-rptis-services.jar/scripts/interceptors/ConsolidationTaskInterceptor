import com.rameses.annotations.*
import com.rameses.eserver.*;
import com.rameses.common.*;

class ConsolidationTaskInterceptor
{
	@Service('ConsolidationService')
	def svc 

	@Service('RPTTaskService')
	def taskSvc

	@Service('RPTTrackingService')
    def trackingSvc

    @Service('RPTWorkflowService')
    def workflowSvc 

    @Service(value="GroupNotificationService", connection="notification_proxy")
    def notificationSvc    


	@Env
	def env 

	def CS_TXN_TYPE = 'CS'

	def createTask(entity, wf){
		return [
			objid		: entity.objid,
			refno		: entity.txnno,
			docname 	: 'Consolidation',
			filetype	: 'consolidation',
			action 		: wf.tostate,
			msg			: wf.message,
			signatory 	: wf.signatory,
			workflowid  : wf.workflowid,
		]
	}


	@Before(pattern="ConsolidationService.deleteConsolidation") 
	public void deleteConsolidation( evt ) {
		def entity = evt.args[0]
		taskSvc.delete(entity)
	}


	@After(pattern="ConsolidationService.createConsolidation") 
	public void createConsolidation( evt ) {
		def consolidation = evt.result
		def startwf = workflowSvc.findStart([docname:'Consolidation', appliedto:CS_TXN_TYPE, fromstate:null])
		def task = createTask(consolidation, startwf)
		task = taskSvc.createTask(task)
		taskSvc.assignTask(task)
		updateSignatoryFromTask(consolidation, task)
	}

	@After(pattern="ConsolidationService.(submitFor.*)") 
	public void handleNextWorkflow( evt ) {
		def consolidation = evt.result 
		def wf = workflowSvc.findNext([docname:'Consolidation', appliedto:CS_TXN_TYPE, fromstate:consolidation.taskaction])
		def task = createTask(consolidation, wf)
		taskSvc.createTask(task)
		consolidation.taskaction = task.action

		//notify group
		def msg = [
			objid     : consolidation.objid,
			senderid  : task.createdby.objid,
			sender 	  : task.createdby.name,
			groupid   : wf.tostate,
			message   : 'Consolidation No. ' + consolidation.txnno  + '    (' + wf.tostate + ')',
			filetype  : 'consolidation',
		]
		notificationSvc.addMessage(msg)
	}


	@After(pattern="ConsolidationService.approveConsolidationAsync") 
	public void approveConsolidationAsync( evt ) {
		taskSvc.closeTask([objid:evt.result.objid])
		trackingSvc.updateMessage([objid:evt.result.objid, msg:'Consolidation is already approved.'])
	}

	@After(pattern="ConsolidationService.approveConsolidation") 
	public void approveConsolidation( evt ) {
		taskSvc.closeTask([objid:evt.result.objid])
	}

	@After(pattern="ConsolidationService.disapproveConsolidation") 
	public void disapproveConsolidation( evt ) {
		taskSvc.closeTask([objid:evt.result.objid])
	}

	@After(pattern="ConsolidationService.disapproveFor.*") 
	public void handleDisapprove( evt ) {
		def consolidation = evt.result
		taskSvc.disapproveTaskByObjid(consolidation.objid, consolidation._disapprovemsg)
	}

	@After(pattern="RPTTaskService.(createNextUserTask|assignTaskToMe)", eval="#{result.filetype == 'consolidation'}") 
	public void updateSignatoryInfo( evt ) {
		def task = evt.result 
		def consolidation = svc.openConsolidation(task.objid)
		updateSignatoryFromTask(consolidation, task)
		notificationSvc.removeMessage([objid:consolidation.objid])
	}

	void updateSignatoryFromTask(consolidation, task){
		if (! consolidation[task.signatory]) 
			consolidation[task.signatory] = [:]

		consolidation[task.signatory].dtsigned = task.startdate 
		consolidation[task.signatory].name = task.assignedto.name 
		consolidation[task.signatory].title = task.assignedto.title
		svc.updateConsolidation(consolidation) 
	}	
}

