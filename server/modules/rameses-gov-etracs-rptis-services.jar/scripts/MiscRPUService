import com.rameses.annotations.*
import com.rameses.util.*

class MiscRPUService
{
    @PersistenceContext('rpt')
    def em

    @Env
    def env
    
    @Service('RPTUtil')
    def util

    @Service('DateService')
    def dateSvc

    @Service('NumberService')
    def numberSvc
    
    @Service('LandRPUService')
    def rpuSvc

    @Service('MiscRYSettingLookupService')
    def miscSettingSvc 

    
    def SCHEMA_RPU     = 'miscrpu'
    def SCHEMA_ITEM    = 'miscrpu:item'
    def SCHEMA_PARAM   = 'miscrpu:param'
    

    @ProxyMethod
    public def initMiscRpu(rpu) {
        def landrpu = rpuSvc.getLandRpuByRealPropertyId(rpu.realpropertyid)
        if (!landrpu) throw new Exception('Land RPU Reference does not exists.')

        rpu.landrpuid   = landrpu.objid 
        rpu.items       = []
        return rpu 
    }

    @ProxyMethod
    public def createMiscRpu( rpu ) {
        def xrpu = EntityUtil.mapToField(rpu)
        em.validate(SCHEMA_RPU, xrpu)
        em.create(SCHEMA_RPU, xrpu)
        saveItems(rpu.items)
        return rpu
    }


    @ProxyMethod
    public def updateMiscRpu(rpu) { 
        def xrpu = EntityUtil.mapToField(rpu)
        em.validate(SCHEMA_RPU, xrpu)
        em.update(SCHEMA_RPU, xrpu)
        saveItems(rpu.items)
        removeDeletedItems(rpu)
        return rpu
    }


    @ProxyMethod
    public def openMiscRpu( objid ) {
        def rpu = em.read(SCHEMA_RPU, [objid:objid])
        if (!rpu) {
            throw new Exception('Miscellaneous RPU does not exists or has been deleted.')
        }
        rpu.actualuse = miscSettingSvc.lookupAssessLevelById(rpu.actualuse.objid)
        rpu.items  = getMiscRpuItems(rpu)
        return rpu 
    }


    @ProxyMethod
    public def deleteMiscRpu(rpu){
        removeDeletedItems(rpu)
        em.sqlContext.createNamedExecutor('miscrpu:deleteAllParams').setParameters(rpu).execute()
        em.sqlContext.createNamedExecutor('miscrpu:deleteAllMiscRpuItems').setParameters(rpu).execute()
        em.delete(SCHEMA_RPU, rpu)
    }

    @ProxyMethod
    public void generateNewIds( rpu ) {
        rpu.items.each{ item ->
            item.objid     = util.generateId('MI')
            item.miscrpuid = rpu.objid 
            
            item.params.each{ p ->
                p.objid = util.generateId('P')
                p.miscrpuitemid = item.objid 
                p.miscrpuid = rpu.objid 
            }
        }
    }


    @ProxyMethod
    public def getClassifications(  ) {
        return rpuSvc.getClassifications()
    }


    @ProxyMethod
    public def getExemptionTypes(){
        return rpuSvc.getExemptionTypes()
    }


    @ProxyMethod
    public def getMiscRySetting(objid){
        return miscSettingSvc.getMiscRySettingById(objid)
    }


    @ProxyMethod
    public def getMiscAssessLevels(params) {
        return miscSettingSvc.lookupAssessLevels(params)
    }




    /*-------------------------------------------------------------------
    *
    * COMPUTATION METHODS
    *
    -------------------------------------------------------------------*/
    @ProxyMethod
    public def calculateAssessment( rpu ) {
        def setting = getRPTSetting()
        rpu.items.each{item->
            calculateMiscItemAssessment(item, rpu, setting)
        }
        rpu.totalbmv    = util.sum(rpu.items, 'basemarketvalue')
        rpu.totalmv     = util.sum(rpu.items, 'marketvalue')
        rpu.totalav     = util.sum(rpu.items, 'assessedvalue')
        return rpu
    }

    @ProxyMethod
    public def calculateExpression( miscitem ) {
        def binding     = util.buildSystemFieldBindings( miscitem );
        binding        += util.buildParamBindings( miscitem );
        return numberSvc.round( util.evaluateExpr( miscitem.expr, binding ) );
    }



    void calculateMiscItemAssessment( miscitem, rpu, rysetting ) {
        validateMiscItemInfo(miscitem)
        
        miscitem.depereciatedvalue  = 0.0;
        miscitem.marketvalue        = 0.0;
        miscitem.assesslevel        = 0.0;
        miscitem.assessedvalue      = 0.0;

        miscitem.depreciatedvalue   = numberSvc.round( miscitem.basemarketvalue * miscitem.depreciation / 100.0 );
        miscitem.marketvalue        = numberSvc.round( miscitem.basemarketvalue - miscitem.depreciatedvalue );
        
        if( util.isTrue(rysetting.roundadjmarketvalue) ) {
            miscitem.marketvalue = numberSvc.roundToTen( miscitem.marketvalue )
        }
        
        miscitem.assesslevel    = calculateAssessLevel(miscitem, rpu);
        miscitem.assessedvalue  = numberSvc.roundToTen( miscitem.marketvalue * miscitem.assesslevel / 100.0 );
        zeroOutAVWhenExempt(miscitem, rpu)
    }


    def calculateAssessLevel( miscitem, rpu ) {
        if(util.isTrue(rpu.actualuse.fixrate)) {
            return rpu.actualuse.rate
        }

        def range = miscSettingSvc.lookupAssessLevelRange([miscassesslevelid:rpu.actualuse.objid, mv:miscitem.marketvalue])
        if( ! range ) throw new Exception('Market Value of P' + numberSvc.format('#,##0.00', miscitem.marketvalue) + ' has no assess level range definition.' )
        return range.rate 
    }

    void validateMiscItemInfo(miscitem) {
        util.required('Depreciation Rate', miscitem.depreciation)
        if (miscitem.depreciation < 0.0) 
            throw new Exception('Depreciation Rate must be greater than or equal to zero.')
    }


    void zeroOutAVWhenExempt(miscitem, rpu){
        if( util.toBoolean(rpu.taxable, true) == false && util.isTrue(var.rpt_zero_av_for_exempt_property) ){
            miscitem.assesslevel = 0.0
            miscitem.assessedvalue = 0.0 
        }
    }


    /*-------------------------------------------------------------------
    *
    * HELPER METHODS
    *
    -------------------------------------------------------------------*/
    
    void saveItems(items){
        items.each{ item -> 
            def xitem = EntityUtil.mapToField(item)
            em.validate(SCHEMA_ITEM, xitem)
            em.save(SCHEMA_ITEM, xitem)

            item.params.each{param ->
                def xparam = EntityUtil.mapToField(param)
                em.validate(SCHEMA_PARAM, xparam)
                em.save(SCHEMA_PARAM, xparam)
            }
        }
    }


    def getMiscRpuItems(rpu){
        def items = em.sqlContext.createNamedQuery('miscrpu:getMiscRpuItems').setParameters(rpu).resultList 

        items.each{ item ->
            item.params = em.sqlContext.createNamedQuery('miscrpu:getMiscRpuItemParams')
                                .setParameter('miscrpuitemid', item.objid)
                                .resultList 
        }

        return items 
    }


    void removeDeletedItems(rpu){
        rpu._params?.each{ 
            it = EntityUtil.mapToField(it)
            em.delete(SCHEMA_PARAM, it)
        }

        rpu._items?.each{ item -> 
            item.params.each{ param ->
                param = EntityUtil.mapToField(param)
                em.delete(SCHEMA_PARAM, param)
            }
            em.delete(SCHEMA_ITEM, item)
        }
    }



    //TODO:
    def getRPTSetting(){
        return [roundadjmarketvalue:0]
    }

    
}
 