<workunit >
    <invokers>
        <invoker folderid="/explorer/report/rpt" action="init" caption="Tax Declaration Batch Printing" target="window" index="602" 
            role="ASSESSOR_REPORT" />
        
        <invoker type="formActions" action="_close" caption="Close" mnemonic="c" icon="images/toolbars/cancel.png" immediate="true" visibleWhen="#{mode == 'init'}"/>
        <invoker type="formActions" action="print" caption="Print" mnemonic="p" shortcut="ctrl + P" icon="images/toolbars/print.png" visibleWhen="#{mode == 'init'}"/>
        <invoker type="formActions" action="cancelPrinting" caption="Cancel Printing" icon="images/toolbars/cancel.png" mnemonic="a" shortcut="ctrl + a" visibleWhen="#{mode == 'printing'}" />
    </invokers>
    <code >
    <![CDATA[
       import com.rameses.rcp.common.*;
        import com.rameses.rcp.annotations.*;
        import com.rameses.osiris2.client.*;
        import com.rameses.osiris2.reports.*;
        import com.rameses.rcp.framework.TaskManager;

        class TaxDecBatchReportController
        {
            @Binding
            def binding

            @Service("ReportParameterService")
            def svcParams

            @Service("TDReportService")
            def svc

            @Service('LGUService')
            def lguSvc
            
            @Service('Var')
            def var 
            
            @Service('FAASListService')
            def faasListSvc
            
            def title='Tax Declaration Batch Printing'

            def lgu;
            def barangays;
            def barangay;
            def section;
            def revisionyear
            def state
            def states

            def msg 
            def mode 
            def faasTask;
            
            public void init() {
                revisionyear = var.current_ry 
                barangays = lguSvc.getBarangaysByParentId(null);
                mode='init';
            }
            
            List getStates(){
                if (!states) states = faasListSvc.getStates();
                return states
            }
            
            def updateMessage = { msg ->
                this.msg = msg
                binding.refresh('msg')
            }

            def onFinish = { msg -> 
                this.msg = msg
                this.mode = 'init';
                binding.refresh();
            }

            def onError = { err ->
                this.mode = 'init';
                binding.refresh();
                MsgBox.err( err );
            }

            def cancelPrinting() {
                faasTask.interrupt = true;
                this.mode = 'init'
                msg = 'Printing of faas has been cancelled.  '
                binding.refresh();
            }

            public void print() {
                mode = 'printing';
                def params = [barangayid: barangay.objid, revisionyear: revisionyear, section:section];
                params.state = state ? state : '%'
                
                def list = svc.getFaasIds(params) 
                if( !list) throw new Exception("No record(s) found.     ")
                faasTask = new FaasBatchTask([
                        updateMessage:updateMessage,
                        onError: onError,
                        onFinish: onFinish, 
                        interrupt: false, 
                        svcParams: svcParams,
                        svc: svc, 
                        faaslist: list 
                     ]);
                Thread t = new Thread( faasTask);
                t.start();
            }

        }

        class FaasBatchTask implements Runnable {

            def svcParams
            def svc;

            def faaslist;
            def reportdata;

            def updateMessage;
            def onError;
            def onFinish;
            boolean interrupt 
            
            def reportPath = 'com/rameses/gov/etracs/rpt/report/td/'

            public void run() {
                try { 
                    while(!interrupt && !faaslist.isEmpty() ) {
                        def o = faaslist.remove(0);
                        reportdata = svc.buildTaxDec( o.objid )

                        report.viewReport()
                        ReportUtil.print( report.report, false) ;

                        updateMessage("Processing TD No. ${o.tdno} "  );
                        Thread.sleep(100)
                    }    
                    def msg = "Finished..... " 
                    if(interrupt) 
                        msg = 'Faas printing has been interrupted.    '
                    onFinish(msg );
               } catch(e) {
                    onError( e );
               }
            }
            
            
            def report = [
                getReportName : { return reportPath + 'tdreport.jasper'},
                getSubReports : { 
                    return [
                        new SubReport( 'TDReportLand',  reportPath + 'tdreportland.jasper'),
                        new SubReport( 'TDReportBldg',  reportPath + 'tdreportbldg.jasper'),
                        new SubReport( 'TDReportMach',  reportPath + 'tdreportimprovement.jasper'),
                        new SubReport( 'TDReportPlant', reportPath + 'tdreportimprovement.jasper'),
                        new SubReport( 'TDReportMisc',  reportPath + 'tdreportimprovement.jasper'),
                        new SubReport( 'TDReportAnnotation', reportPath + 'tdreportannotation.jasper'),
                    ] as SubReport[]
                },
                getReportData : { return reportdata },
                getParameters : { svcParams.getStandardParameter() },
            ] as ReportModel 

        }
        
    ]]>    
    </code>
     <pages>
        <page  template="com.rameses.gov.etracs.rpt.report.FAASBatchReportPage" />
    </pages>
</workunit>